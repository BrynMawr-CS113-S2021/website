include::header.adoc[]

:title: Jam 3: Study Guide
:Author Initials: ASN
:Date:    2020-08-15
:Revision: 0.1

= {title}

Jams are open book. 40 minutes in lab.

Topics:

This quiz includes the previous topics from Quiz 1-2 plus

* loops: for- and while-
* `sin` and `cos`
* functions: defining and calling, return types, return statements, parameters (aka arguments)
* scope of a variable

= Practice questions

1) Write a program that draws the numbers 1-10 in a circle formation

image:Q3-dial.png[width=200px]

2) Write a function that computes the area of a rectangle. Your function should take the height and width as parameters and return a float. Call your function from setup() using 10 and 5 as parameters.

image:Q3-happy.png[width=200px]

3) Write a function that draws text with a circle around it. Your function should take a String as the parameter and uses the `textWidth` function to set the size of the circle. Call your function from setup() using "happy" as a parameter.

4) Write a function that inputs a month as an integer and returns a string name for that month. For example, 
if we call the function with the number 1, the function should return "January". Call your function from setup() for each month of the year (use a loop) and print the results to the console.

5) Consider the following loop

[source,java]
----
int pinLength = 4;
String pin = "";
for (int i = 0; i < pinLength; i++) {
  pin = pin + "*";
  println(pin);
}
println("Enter your pin: "+pin);
----

* What is output to the console when we run this program?
* Convert the loop above so it uses a while loop instead of a for loop

6) Consider the following program. It's not running properly. Why not?  Modify the program to fix it.

[source,java]
----
void setup() {
  size(500,500);
  float size = 400;
  for (float alpha = 0; alpha > 0; alpha += 10) {
    fill(255,0,0,alpha);
    rect(250, 250, size, size);
    size -= 50;
  }
}
----

7) Consider the following program

[source,java]
----
String userText = "Happy Halloween!";

String mystery2(String message, String symbol) {
  message = symbol+message+symbol;
  // (a) Show function stack here
  return message;
}

void mystery1(String message, float x, float y) {
  println(message);
  message = mystery2(message, "~~");
  text(message, x, y-50);
  text(message, x, y+50);
  println(message);
  // (b) Show function stack here
}

void setup() {
  size(500,500);
  mystery1(userText, width * 0.5, height * 0.25); 
  println(userText);
}
----

* Show the function stack starting at `setup()` and ending with the comment labeled (a).
* Show the function stack starting at `setup()` and ending with the comment labeled (b).
* What global variables are used by this program?
* What are the parameters to mystery1?
* What variables are in scope in setup?
* What variables are in scope in function mystery1?
* What variables are in scope in mystery2?
* What is the console output of this program?
* What text is drawn to the canvas?

= Answers

1) 
[source,java]
----
void setup() {
  size(500,500);
  textSize(32);
  textAlign(CENTER);
  fill(0);
  
  for (int i = 0; i < 10; i++) {
    float angle = 2 * PI/10.0 * i;
    float x = 250 + 100 * cos(angle);
    float y = 250 + 100 * sin(angle);
    text(i+1, x, y);
  }
}
----


2) 
[source,java]
----
float computeArea(float w, float h) {
  return w * h;
}

void setup() {
  size(500,500);
  float area = computeArea(10, 5); 
}
----

3) 
[source,java]
----
void drawCircleText(String message) { // you could pass x,y but not required
  float w = textWidth(message);
  fill(255);
  ellipse(250,250,w*2,w*2);
  fill(0);
  text(message, 250, 250);
}

void setup() {
  size(500,500);
  textAlign(CENTER); // not required but looks nicer
  textSize(64); // not required but looks nicer
  drawCircleText("happy");
}
----

4) 
[source,java]
----
// NOTE: I will not ask you to write a long `if` statement on a quiz
String monthToString(int month) {
  if (month == 1) {
    return "January";
  }
  else if (month == 2) {
    return "February";
  }
  else if (month == 3) {
    return "March";
  }
  else if (month == 4) {
    return "April";
  }
  else if (month == 5) {
    return "May";
  }
  else if (month == 6) {
    return "June";
  }
  else if (month == 7) {
    return "July";
  }
  else if (month == 8) {
    return "August";
  }
  else if (month == 9) {
    return "September";
  }
  else if (month == 10) {
    return "October";
  }
  else if (month == 11) {
    return "November";
  }
  else if (month == 12) {
    return "December";
  }
  return "Unknown";
}

void setup() {
  size(500,500);
  for (int i = 1; i <= 12; i++) {
    String month = monthToString(i);
    println(month);
  }
}
----

NOTE: It is easier to implement this function using arrays

[source,java]
----
String monthToString(int month) {
  String[] months = {
    "January", "February", "March",
    "April", "May", "June", "July",
    "August", "September", "October",
    "November", "December"
  };
  return months[month-1]; // watch out for index!
}
----

5a)
[source]
----
*
**
***
****
Enter your pin: ****
----

5b)
[source,java]
----
  int pinLength = 4;
  String pin = "";
  int i = 0;
  while (i < pinLength) {
    pin = pin + "*";
    println(pin);
    i++;
  }
  println("Enter your pin: "+pin);  
----

6) We never go inside the for loop because `alpha` is never greater than zero. 
One way we could fix it is to change the condition to `alpha < 255`.

7a) link:Q3-PracticeStack.pdf[Stack Drawings + answers] 